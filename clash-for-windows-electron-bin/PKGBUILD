# Maintainer: ArcanusNEO <admin@transcliff.eu.org>
# Contributor: sukanka <su975853527 [AT] gmail.com>
pkgname=clash-for-windows-electron-bin
_pkgname=${pkgname%-bin}
pkgver=0.20.39
pkgrel=2
pkgdesc="A Windows/macOS/Linux GUI based on Clash and Electron. Using system clash and electron"
arch=("x86_64" "aarch64")
license=('unknown')
url="https://github.com/Fndroid/clash_for_windows_pkg"
install=clash-for-windows-bin.install
provides=('clash-for-windows' ${_pkgname})
conflicts=('clash-for-windows' 'clash-for-windows-bin')
depends=('electron25' 'clash-geoip')
optdepends=(
  'nftables: TUN mode required.'
  'iproute2: TUN mode required.'
  'clash-premium-bin: clash core'
  'clash-meta: clash core'
)
makedepends=('asar' 'npm')
# source_x86_64=("${pkgname}-${pkgver}-x86_64-linux.tar.gz::${url}/releases/download/${pkgver}/Clash.for.Windows-${pkgver}-x64-linux.tar.gz")
source_x86_64=("${pkgname}-${pkgver}-x86_64-linux.tar.gz::https://web.archive.org/web/20231020093608if_/https://objects.githubusercontent.com/github-production-release-asset-2e65be/153697551/c6e9c14e-0049-4e02-8ea5-12d8fabf9f9d?X-Amz-Algorithm=AWS4-HMAC-SHA256&X-Amz-Credential=AKIAIWNJYAX4CSVEH53A%2F20231020%2Fus-east-1%2Fs3%2Faws4_request&X-Amz-Date=20231020T093608Z&X-Amz-Expires=300&X-Amz-Signature=f07dabe509fbcb04ed7d0491a41bce68417ce24f04792124b300344c623114b4&X-Amz-SignedHeaders=host&actor_id=0&key_id=0&repo_id=153697551&response-content-disposition=attachment%3B%20filename%3DClash.for.Windows-0.20.39-x64-linux.tar.gz&response-content-type=application%2Foctet-stream")
# source_aarch64=("${pkgname}-${pkgver}-aarch64-linux.tar.gz::${url}/releases/download/${pkgver}/Clash.for.Windows-${pkgver}-arm64-linux.tar.gz")
source_aarch64=("${pkgname}-${pkgver}-aarch64-linux.tar.gz::https://web.archive.org/web/20231020093527if_/https://objects.githubusercontent.com/github-production-release-asset-2e65be/153697551/b1e7558e-9fa3-4ccc-b2f6-d0ff6cc5b34a?X-Amz-Algorithm=AWS4-HMAC-SHA256&X-Amz-Credential=AKIAIWNJYAX4CSVEH53A%2F20231020%2Fus-east-1%2Fs3%2Faws4_request&X-Amz-Date=20231020T093527Z&X-Amz-Expires=300&X-Amz-Signature=4942053a395a115dcea01b4ee9854fc57a01b0d5ebf8eab99b0933a29e155f12&X-Amz-SignedHeaders=host&actor_id=0&key_id=0&repo_id=153697551&response-content-disposition=attachment%3B%20filename%3DClash.for.Windows-0.20.39-arm64-linux.tar.gz&response-content-type=application%2Foctet-stream")
source=(
  "clash-for-windows.desktop"
  "cfw"
  "cfw-start-pre.sh"
  "clash-core-service@.service")
sha256sums=(
  '9a84b05b718913d3d1fbe462a2da671abff2cb94bb2ded75a3af12204cebe32f'
  'ed6792e17a787057891408f24f6bedcdda60864d0420bb636d2cc6d6d616ef72'
  '4bea2fe6e8580a4316451ca5c8b921bd908422d839f8bd9ceaddd709f9178153'
  '2d7965b10f0abc8302e7514a8975e1cbfe1058511b2c56717790113b325ef671'
)
sha256sums_x86_64=('e07c5e358bce99511c103262ba0d6d0167c70242f2e68827b09f7a2918d43dc0')
sha256sums_aarch64=('e328abfe68c122c396f7296bb610934f9ca407af97b0f50d99ff62d43aaf63e0')

_parch=$(echo ${arch} | sed "s/x86_64/x64/;s/aarch64/arm64/")
build() {
  cd $srcdir
  sed -i "s/pkgver/${pkgver}/" clash-for-windows.desktop

  cd "Clash for Windows-${pkgver}-${_parch}-linux"/resources/
  asar e app.asar apps

  # fix for autostart and system electron
  cd apps
  sed -i 's|r=n\[1\],|r="cfw\\nIcon=clash\\n",|g' dist/electron/renderer.js
  # force local mode
  # sed -i 's|200===a.status|true|g' dist/electron/renderer.js
  sed -i 's|"electron-log": "^4.1.0",|"electron-log": "^4.4.6",|g' package.json

  export HOME=$srcdir
  npm install
  cd ..
  asar p apps app.asar
}

package() {
  cd $srcdir

  install -Dm755 cfw -t ${pkgdir}/usr/bin
  install -Dm644 "clash-for-windows.desktop" -t ${pkgdir}/usr/share/applications
  install -Dm644 "clash-core-service@.service" -t ${pkgdir}/usr/lib/systemd/system/
  install -Dm755 cfw-start-pre.sh -t ${pkgdir}/opt/clash-for-windows

  cd "Clash for Windows-${pkgver}-${_parch}-linux"
  install -Dm644 resources/app.asar -t ${pkgdir}/opt/clash-for-windows/

  cp -pvr resources/static ${pkgdir}/opt/clash-for-windows/

  cd resources/apps/dist/electron/static/imgs
  install -Dm644 logo.png ${pkgdir}/usr/share/icons/hicolor/512x512/apps/clash.png

  cd ${pkgdir}/opt/clash-for-windows/static/files
  rm -rf linux/{common,${_parch}/clash-linux} default/Country.mmdb
  ln -s /usr/bin/clash linux/${_parch}/clash-linux
  ln -s /etc/clash/Country.mmdb default/Country.mmdb
}
