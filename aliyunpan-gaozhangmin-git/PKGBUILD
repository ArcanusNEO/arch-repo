# Maintainer: ArcanusNEO <admin@transcliff.eu.org>
# Contributor: zhullyb < zhullyb [at] outook dot com >
# Contributor: sukanka < su975853527 [at] gmail dot com >

pkgname=aliyunpan-gaozhangmin-git
url="https://github.com/gaozhangmin/aliyunpan"
pkgrel=1
pkgver=r368.aaf64ff
pkgdesc="阿里云盘小白羊v3，现由 gaozhangmin 维护"
arch=("any")
license=("MIT")
_electron=electron
depends=("$_electron" 'aria2')
optdepends=('mpv: media preview support')
makedepends=('git' 'npm' 'yarn')
provides=("aliyunpan")
conflicts=("aliyunpan")
source=(
  "git+${url}.git"
  'alixby.desktop'
)
b2sums=(
  'SKIP'
  '4680694587128108d004ee46d494189704bc598b25c90cf1269912ce37e8680eb5f7e0089b83d0642feb9e699cafcf17cc9bc5b96f166255aeb7f00db8a55d22'
)

pkgver() {
  cd ${srcdir}/aliyunpan
  printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
  cat <<EOF >alixby.sh
#!/bin/sh
set -eu

export ELECTRON_FORCE_IS_PACKAGED=true
export ELECTRON_IS_DEV=0

exec $_electron --no-sandbox /usr/lib/alixby/app.asar "\$@"
EOF
}

build() {
  cd ${srcdir}/aliyunpan
  yarn
  yarn run build:electron --linux dir
}

package() {
  install -Dm644 ${srcdir}/aliyunpan/release/linux-unpacked/resources/app.asar -t ${pkgdir}/usr/lib/alixby/
  cp -a ${srcdir}/aliyunpan/release/linux-unpacked/resources/crx/ ${pkgdir}/usr/lib/alixby/

  mkdir -p ${pkgdir}/usr/lib/alixby/engine
  install -Dm644 ${srcdir}/aliyunpan/release/linux-unpacked/resources/engine/aria2.conf -t ${pkgdir}/usr/lib/alixby/engine/
  ln -s /usr/bin/aria2c ${pkgdir}/usr/lib/alixby/engine/

  cp -a ${srcdir}/aliyunpan/release/linux-unpacked/resources/images/ ${pkgdir}/usr/lib/alixby/
  mkdir -p ${pkgdir}/usr/share/icons
  ln -s /usr/lib/alixby/images/icon_256x256.png ${pkgdir}/usr/share/icons/alixby.png

  install -Dm644 ${srcdir}/alixby.desktop -t ${pkgdir}/usr/share/applications/
  install -Dm755 ${srcdir}/alixby.sh ${pkgdir}/usr/bin/alixby
}
